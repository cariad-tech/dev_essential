# Copyright @ 2021 VW Group. All rights reserved.
#
# This Source Code Form is subject to the terms of the Mozilla
# Public License, v. 2.0. If a copy of the MPL was not distributed
# with this file, You can obtain one at https://mozilla.org/MPL/2.0/.

# standalone vs integrated
if(dev_essential_cmake_enable_integrated_tests)
    set(_interface_include_directories ${PROJECT_SOURCE_DIR}/include)
else()
    # All public header files have one common include dir distributed by dev_essential::base
    find_package(dev_essential REQUIRED)
    get_target_property(_interface_include_directories dev_essential::base INTERFACE_INCLUDE_DIRECTORIES)
endif()

# Create the template cpp file, later configured to one cpp file containing one public header
set(_test_filepath_no_ext ${CMAKE_CURRENT_BINARY_DIR}/generated/test_self_contained_header)
file(WRITE ${_test_filepath_no_ext}.cpp.in
     "//Auto generated by CMake in file: ${CMAKE_CURRENT_LIST_FILE}\n"
     "#include \"@_header_file@\"\n")

# Get all public header files and configure the corresponding *.cpp file including this header
file(GLOB_RECURSE _header_files ${_interface_include_directories}/*.h)
foreach(_header_file ${_header_files})
    math(EXPR _file_number "${_file_number}+1")
    file(RELATIVE_PATH _relative_path_no_ext ${_interface_include_directories} ${_header_file})
    string(REGEX REPLACE "[/\\. ]" "_" _relative_path_no_ext ${_relative_path_no_ext})
    configure_file(${_test_filepath_no_ext}.cpp.in ${_test_filepath_no_ext}_${_file_number}.cpp)
    set(_test_files ${_test_files} ${_test_filepath_no_ext}_${_file_number}.cpp)
endforeach()

# Compile and link all generated cpp test files to one executable and create a test
add_library(self_contained_header_files_test OBJECT ${_test_files})
set(_dev_essential_lib_to_link
    $<IF:$<TARGET_EXISTS:dev_essential::pkg_rpc>,dev_essential::pkg_rpc,dev_essential::preprocessor>)
target_link_libraries(self_contained_header_files_test PRIVATE ${_dev_essential_lib_to_link})
set_target_properties(self_contained_header_files_test
                      PROPERTIES STATIC_LIBRARY_OPTIONS $<$<CXX_COMPILER_ID:MSVC>:/ignore:4221>
                                 FOLDER test/sca)
target_compile_definitions(self_contained_header_files_test PRIVATE DEV_ESSENTIAL_DISABLE_DEPRECATED_WARNINGS)